# SD Systems Banked CP/M 3 test for SIMH/AltairZ80
#
# Copyright (c) 2025 Patrick Linstruth
# https://github.com/deltecent
#
# Boot SD Systems Banked CP/M 3
#

set env A8800_TEST_NAME=SD Systems Banked CP/M 3 Test
set env A8800_TEST_BIN=sds_cpm3
echof "\r\n*** %SIM_NAME%: %A8800_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"
if not exist %A8800_TEST_BIN%.zip curl "https://raw.githubusercontent.com/deltecent/altair8800-packages/master/%A8800_TEST_BIN%/%A8800_TEST_BIN%.zip" --output %A8800_TEST_BIN%.zip
!unzip -o %A8800_TEST_BIN%.zip '*.dsk'

noexpect
set runlimit 100M instructions
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

set rom nodbl
set bram ena
set bram eram
set dsk dis

;set debug stdout

; Configure CP/M 3
set bram addpage=0-bf

; Configure SBC200 and Interrupts
set sbc200 ena
set sbc200 console

; Configure disk controller for SD Systems CP/M 2
set vfii en
;dep dsmask 80

; Attach CP/M 2.2 disk images
att vfii0 SDS_CPM3_60K.dsk
att vfii1 SDS_CPM3_BLANK.dsk

:CPM3_FLOPPY_TEST
echof "\r\n*** SD Systems Banked CP/M 3 Test"

; Boot CP/M 3
noexpect
expect "A>"; goto RUNTESTS
g f000
goto FAIL

:RUNTESTS
call FLPYFORMAT 	; Format B: SSDD
call FLPYCOPY 220
call GENCPM
call COPYSYS
call REBOOT 01
goto PASS

:FLPYFORMAT
; Format floppy <drive> <density> <sided>
noexpect
expect "A>" ; send "FORMAT\n" ; go -q
expect "Enter Drive To Format  (A..P)" ; send "\r" ; go -q
expect "Do You Wish to Format Drive <B>" ; send "\r" ; go -q
expect "Single, or Double Density" ; send "\r" ; go -q
expect "5\"  or 8\"" ; send "\r" ; go -q
expect "Single, or Double Sided" ; send "\r" ; go -q
expect ":77" ; send "77" ; go -q
expect "Enter <Return> to Continue, or Q to Quit" ; send "\r" ; go -q
expect "Format Completed" ; go -q
expect "Do You Wish To Format another Diskette ? (Y/N)" ; send "\r" ; go -q
expect "A>" ; send "\003"; go -q
expect "A>" ; return
send "\r"
go -q
goto FAIL

:FLPYCOPY
noexpect
expect "A>" ; send "PIP B:=A:*.*\r" ; go -q
expect "A>" ; send "SHOW B:\r" ; go -q
expect "B: %1k" ; go -q
expect "A>"; return
send "\r"
go -q
goto FAIL

:GENCPM
noexpect
expect "A>" ; send "GENCPM AUTO DISPLAY\r" ; go -q
expect "*** CP/M 3.0 SYSTEM GENERATION DONE ***" ; go -q
expect "A>" ; return
send "\r"
go -q
goto FAIL

:COPYSYS
noexpect
expect "A>" ; send "COPYSYS COLD.COM\r" ; go -q
expect "Destination drive name (or return to reboot)" ; send "B" ; go -q
expect "Destination on B then type return" ; send "\r" ; go -q
expect "Function complete" ; go -q
expect "Do you wish to copy CPM3.SYS?" ; send "N" ; go -q
expect "A>" ; send "\r" ; return
send "\r"
go -q
goto FAIL

:REBOOT
noexpect
expect "\r\n." ; send "C%1\r"; go -q
expect "A>" ; send "DIR [sort]\r"; go -q
expect "Press RETURN to Continue"; send "\r"; go -q
expect "A>" ; return
reset cpu
reset sbc200
g e000
go -q
goto FAIL

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %A8800_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
detach vfii0
detach vfii1

set vfii disabled
set -f sbc200 disabled

# Remove disk images
del SDS_CPM3_60K.dsk
del SDS_CPM3_BLANK.dsk
;del sds_cpm3.zip

echof "\n\r*** PASSED - %SIM_NAME%: %A8800_TEST_NAME%.\n"

:DONE
