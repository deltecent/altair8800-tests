# SD Systems CP/M 2.2 test for SIMH/AltairZ80
#
# Copyright (c) 2025 Patrick Linstruth
# https://github.com/deltecent
#
# Boot SD Systems CP/M 2.2
#

set env A8800_TEST_NAME=SD Systems CP/M 2.2 Test
set env A8800_TEST_BIN=sds_cpm2
echof "\r\n*** %SIM_NAME%: %A8800_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"
if not exist %A8800_TEST_BIN%.zip curl "https://raw.githubusercontent.com/deltecent/altair8800-packages/master/%A8800_TEST_BIN%/%A8800_TEST_BIN%.zip" --output %A8800_TEST_BIN%.zip
!unzip -o %A8800_TEST_BIN%.zip '*.dsk'

noexpect
set runlimit 100M instructions
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

set rom nodbl
set dsk dis
;set debug stdout

; Configure SBC200 and Interrupts
set sbc200 ena
set sbc200 console
dep SBCRXINTEN 1
dep SBCRXVEC 3
dep SBCRXDBVAL 82

; Configure disk controller for SD Systems CP/M 2
set vfii en
;dep dsmask 80

; Display Bus Configuration
show bus config

; Attach CP/M 2.2 disk images
att vfii0 SDS_CPM2_64K.dsk
att vfii1 SDS_CPM2_BLANK.dsk

:SDS_CPM_TEST
echof "\r\n*** SD Systems CP/M 2 Test"

; Boot CP/M
noexpect
expect "A>"; goto RUNTESTS
g f000
goto FAIL

:RUNTESTS
call FLPYFORMAT B C	; Format B: SSDD
call FLPYCOPY 52
call SYSGEN A B
call REBOOT 01
goto PASS

:FLPYFORMAT
; Format floppy <drive> <density> <sided>
noexpect
expect "A>" ; send "FORMAT\n"; go -q
expect "ENTER DRIVE TO BE FORMATTED " ; send "%1\n"; go -q
expect "DISKETTE TYPE: " ; send "%2" ; go -q
expect "FORMAT FINISHED" ; send "\r" ; go -q
expect "A>" ; send "\003"; go -q
expect "A>" ; return
send "\n"
go -q
goto FAIL

:FLPYCOPY
noexpect
expect "A>" ; send "PIP B:=A:*.*\r" ; go -q
expect "A>" ; send "STAT B:\r" ; go -q
expect "B: %1k" ; go -q
expect "A>" ; return
send "\n"
go -q
goto FAIL

:SYSGEN
noexpect
expect "A>" ; send "SYSGEN\r" ; go -q
expect "SOURCE DRIVE NAME"; send "%1" ; go -q
expect "SOURCE ON A, THEN TYPE RETURN" ; send "\r"; go -q
expect "FUNCTION COMPLETE" ; go -q
expect "DESTINATION DRIVE NAME" ; send "%2" ; go -q
expect "DESTINATION ON B, THEN TYPE RETURN" ; send "\r" ; go -q
expect "FUNCTION COMPLETE" ; go -q
expect "DESTINATION DRIVE NAME" ; send "\r" ; go -q
expect "A>" ; return
send "\n"
go -q
goto FAIL

:REBOOT
noexpect
expect "\r\n." ; send "C%1\r"; go -q
expect "A>" ; return
reset cpu
reset sbc200
g e000
go -q
goto FAIL

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %A8800_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
detach vfii0
detach vfii1

set vfii disabled
set -f sbc200 disabled

# Remove disk images
del SDS_CPM2_64K.dsk
del SDS_CPM2_BLANK.dsk
#del sds_cpm2.zip

echof "\n\r*** PASSED - %SIM_NAME%: %A8800_TEST_NAME%.\n"

:DONE
