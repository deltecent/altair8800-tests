# SIO test for SIMH/Altair8800
#
# Copyright (c) 2025 Patrick Linstruth
# https://github.com/deltecent
#
# SIO echo test
#

set env A8800_TEST_NAME=SIO Test
set env A8800_TEST_BIN=sio_test
echof "\r\n*** %SIM_NAME%: %A8800_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"
if not exist %A8800_TEST_BIN%.zip curl "https://raw.githubusercontent.com/deltecent/altair8800-packages/master/%A8800_TEST_BIN%/%A8800_TEST_BIN%.zip" --output %A8800_TEST_BIN%.zip
!unzip -o %A8800_TEST_BIN%.zip "*.HEX"

noexpect
set runlimit 20M instructions
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

; Configure the Simulator
set sio enable
set sio console

:SIO_TEST
echof "\r\n*** Altair8800 SIO Test"

:RUNTESTS
call SIO
call SS1
goto PASS

; SIO board
:SIO
noexpect
expect "ALTAIR8800 SIO SIMULATOR!\r\n"; return
set sio board=sio
show sio config
hexload SIOECHO.HEX
dep PC 0
send "ALTAIR8800 SIO SIMULATOR!\r\n"
go -q
goto FAIL

; SS1 board
:SS1
noexpect
expect "ALTAIR8800 SS1 SIMULATOR!\r\n"; return
set sio board=ss1
show sio config
hexload SS1ECHO.HEX
dep PC 0
send "ALTAIR8800 SS1 SIMULATOR!\r\n"
go -q
goto FAIL

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %A8800_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
set sio disable
set m2sio0 console

# Remove files
del *.HEX
;del %A8800_TEST_BIN%.zip

echof "\n\r*** PASSED - %SIM_NAME%: %A8800_TEST_NAME%.\n"

:DONE
