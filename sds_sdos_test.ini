# SD Systems SDOS test for SIMH/S100
#
# Copyright (c) 2025 Patrick Linstruth
# https://github.com/deltecent
#
# Boot SDOS
#

set env A8800_TEST_NAME=SD Systems SDOS Test
set env A8800_TEST_BIN=sds_sdos
echof "\r\n*** %SIM_NAME%: %A8800_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"
if not exist %A8800_TEST_BIN%.zip curl "https://raw.githubusercontent.com/deltecent/altair8800-packages/master/%A8800_TEST_BIN%/%A8800_TEST_BIN%.zip" --output %A8800_TEST_BIN%.zip
!unzip -o %A8800_TEST_BIN%.zip '*.dsk'

noexpect
set runlimit 100M instructions
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

set rom nodbl
set dsk dis

;set debug stdout

; Configure SBC200
set sbc200 ena
set sbc200 console

; Configure disk controller for SDOS
set vfii en
;dep dsmask 80
att vfii0 SDOS-56K.dsk
att vfii1 SDOS-BLANK.dsk

:SDS_SDOS_TEST
echof "\r\n*** SD Systems SDOS Test"

; Boot SDOS
expect "\r\n[A]" ; goto RUNTESTS
expect "\r\n." ; goto FAIL
g f000
goto FAIL

:RUNTESTS
call FLPYFORMAT C	; Format B: SSDD
call FLPYCOPY 56 59
goto PASS

:FLPYFORMAT
; Format floppy <drive> <density> <sided>
noexpect
expect "[A]" ; send "FORMAT\r" ; go -q
expect "DISKETTE TYPE: " ; send "%1" ; go -q
expect "FORMAT FINISHED" ; send "\r" ; go -q
expect "[A]" ; send "\003" ; go -q
expect "[A]" ; return
send "\r"
go -q
goto FAIL

:FLPYCOPY
noexpect
expect "[A]" ; send "XFER B:=A:*.*\r" ; go -q
expect "[A]" ; send "XDIR B:\r" ; go -q
expect "%1 entries listed" ; go -q
expect "[A]" ; send "XSTAT B:\r" ; go -q
expect "%2 Directory Entries" ; go -q
expect "[A]" ; return
send "\r"
go -q
goto FAIL

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %A8800_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
detach vfii0
detach vfii1

set vfii disabled
set -f sbc200 disabled

# Remove disk images
del SDOS-56K.dsk
del SDOS-BLANK.dsk
;del sds_sdos.zip

echof "\n\r*** PASSED - %SIM_NAME%: %A8800_TEST_NAME%.\n"
goto DONE

:DONE
