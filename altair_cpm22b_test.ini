# MITS 88-DCDD Buffered CP/M 2.2 test for SIMH/Altair8800
#
# Copyright (c) 2025 Patrick Linstruth
# https://github.com/deltecent
#
# Boot CP/M 2.2
#

set env A8800_TEST_NAME=Buffered CP/M 2.2 Test
set env A8800_TEST_BIN=altair_cpm22b
echof "\r\n*** %SIM_NAME%: %A8800_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"
if not exist %A8800_TEST_BIN%.zip curl "https://raw.githubusercontent.com/deltecent/altair8800-packages/master/%A8800_TEST_BIN%/%A8800_TEST_BIN%.zip" --output %A8800_TEST_BIN%.zip
!unzip -o %A8800_TEST_BIN%.zip "*.DSK"

noexpect
set runlimit 20M instructions
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

; Configure the Simulator
set cpu 8080

; Attach CP/M 2.2 disk images
att dsk0 CPM22B23-56K.DSK
att dsk1 BASICGAMES3-ALTAIR-8IN.DSK

:MITS_CPM2_TEST
echof "\r\n*** MITS CP/M 2.2B Test"

; Boot CP/M
noexpect
expect "A>"; goto RUNTESTS
boot dsk0
goto FAIL

:RUNTESTS
call LISTB
call MBASIC
goto PASS

; List files on Drive B
:LISTB
noexpect
expect "A>"; send "ls b:\n"; go -q
expect "2K bytes remain on B:"; go -q
expect "A>"; return
send "\n"
go -q
goto FAIL

; Load MBASIC
:MBASIC
noexpect
expect "A>"; send "mbasic\n"; go -q
expect "Ok"; send "LOAD \"B:ELIZA.BAS\"\r\n"; go -q
expect "Ok"; send "RUN\r\n"; go -q
expect "?"; send "\003"; go -q
expect "Ok"; send "SYSTEM\r\n"; go -q
expect "A>"; return
send "\n"
go -q
goto FAIL

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %A8800_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
detach dsk0
detach dsk1

# Remove disk images
del CPM22B23-56K.DSK
del BASICGAMES3-ALTAIR-8IN.DSK
del altair_cpm22b.zip

echof "\n\r*** PASSED - %SIM_NAME%: %A8800_TEST_NAME%.\n"

:DONE
