# SIMH CP/M 2.2 test for SIMH/Altair8800
#
# Copyright (c) 2025 Patrick Linstruth
# https://github.com/deltecent
#
# Boot CP/M 2.2 and Test R and W commands
#

set env A8800_TEST_NAME=SIMH CP/M 2.2 Test
set env A8800_TEST_BIN=altair_cpm22b
echof "\r\n*** %SIM_NAME%: %A8800_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"
if not exist %A8800_TEST_BIN%.zip curl "https://raw.githubusercontent.com/deltecent/altair8800-packages/master/%A8800_TEST_BIN%/%A8800_TEST_BIN%.zip" --output %A8800_TEST_BIN%.zip
!unzip -o %A8800_TEST_BIN%.zip "*.DSK"

noexpect
set runlimit 20M instructions
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

; Configure the Simulator
set cpu 8080

; Attach CP/M 2.2 disk images
att dsk0 CPM22B23-56K.DSK
att dsk1 BASICGAMES3-ALTAIR-8IN.DSK

:MITS_CPM2_TEST
echof "\r\n*** SIMH CP/M 2.2 Test"

; Boot CP/M
noexpect
expect "A>"; goto RUNTESTS
boot dsk0
goto FAIL

:RUNTESTS
call WRITE
call READ
goto PASS

; Write a file to the host
:WRITE
noexpect
expect "A>"; send "W MBASIC.COM\n"; go -q
expect "23.75kB written (Binary).\r\n\r\nA>"; send "ERA MBASIC.COM\n"; go -q
expect "A>"; send "MBASIC\n"; go -q
expect "MBASIC?\r\n\r\nA>"; return
send "\n"
go -q
goto FAIL

; Read a file from the host
:READ
noexpect
expect "A>"; send "R MBASIC.COM\n"; go -q
expect "A>"; send "MBASIC\n"; go -q
expect "23.75kB written.\r\n\r\nA>"; send "MBASIC\n"; go -q
expect "Ok"; send "SYSTEM\r\n"; go -q
expect "A>"; return
send "\n"
go -q
goto FAIL

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %A8800_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
detach dsk0
detach dsk1

# Remove disk images
del CPM22B23-56K.DSK
del BASICGAMES3-ALTAIR-8IN.DSK
del MBASIC.COM
del altair_cpm22b.zip

echof "\n\r*** PASSED - %SIM_NAME%: %A8800_TEST_NAME%.\n"

:DONE
